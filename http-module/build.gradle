plugins {
    id "com.github.maiflai.scalatest" version "${scalaTestPluginVersion}"
    id 'dataweave' version "${weaveGradlePluginVersion}"
}

dependencies {
    api project(":file-module")
    // Add the jar file to compileOnly as it requires to access resources (also generated ones)
    compileOnly files(project(":file-module").tasks.jar.outputs.files)
    
    implementation group: 'org.mule.weave', name: 'runtime', version: weaveVersion
    implementation group: 'org.mule.weave', name: 'core-modules', version: weaveVersion

    testImplementation group: 'org.mule.weave', name: 'runtime-test-helpers', version: weaveVersion
    testImplementation (group: 'org.scalatest', name: 'scalatest_2.12', version: scalaTestVersion) {
        exclude group: 'org.scala-lang', module: 'scala-library'
    }
    testRuntimeOnly 'com.vladsch.flexmark:flexmark-all:0.62.2'

    testImplementation project(":http-netty-module")
}

tasks.withType(Copy).all {
    duplicatesStrategy 'exclude'
}

sonarqube {
    properties {
        property "sonar.scala.coverage.reportPaths", "build/reports/scoverageTest/scoverage.xml"
        property 'sonar.exclusions', 'src/main/java/**/*'
    }
}

dataweave {
    template = "markdown,assciidoc"
}

task docs(dependsOn: weavedoc, type: Zip) {
    archiveClassifier = "weavedocs"
    from 'build/docs/weavedocs/'
}

build.dependsOn("weavedoc")

processResources {
    dependsOn "weavecompile"
    from "build/generated/dataweave"
}